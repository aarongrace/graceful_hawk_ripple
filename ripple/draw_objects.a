	USE	"hawk.h"	
	USE	"stdio.h"
	USE	"stdlib.h"
	USE	"common.h"

	EXT	DRAW_ON_CANVAS
	EXT	GENERATE_CIRCLE




CIRCLE_ARRAY=4
PX_LEFT=8
CIRCLE_CHAR=16
CIRCLE_POINTER=20
ARSIZE=24
DRAW_CIRCLE:
	; takes R3=rad,R4,center_x,R5=center_y,R6=char
	C_S
	STORE	R6,R2,CIRCLE_CHAR-ARSIZE
	TOSUBR	GENERATE_CIRCLE
	STORE	R3,R2,CIRCLE_POINTER-ARSIZE


	LOADS	R4,R3
	STORE	R4,R2,PX_LEFT-ARSIZE
	ADDSI	R3,4
	STORE	R3,R2,CIRCLE_ARRAY-ARSIZE

	MOVE	R7,R3 ; R3 out of the way for param input
DC_LP:
	LOADS	R3,R7	; loading coord word into R3 and converting to two reg coords
	TOSUBR	CONVERT_WORD_COORDS

	LOAD	R3,R2,CIRCLE_CHAR-ARSIZE ; loading circle char into R3
	TOSUBR	DRAW_ON_CANVAS

	LOAD	R7,R2,CIRCLE_ARRAY-ARSIZE	; move to the next coord in array
	ADDSI	R7,4
	STORE	R7,R2,CIRCLE_ARRAY-ARSIZE

	LOAD	R6, R2, PX_LEFT-ARSIZE ; px_left -= 1
	ADDSI	R6,-1		; if (px_left == 0) return
	STORE	R6,R2,PX_LEFT-ARSIZE
	TESTR	R6
	BNE	DC_LP

	LOAD	R3,R2,CIRCLE_POINTER-ARSIZE
	TOSUBR	FREE
	R_S

	INT	DRAW_OBJECTS
;	S	MAIN_STOP
	;S	DEBUG
ARSIZE=4
DRAW_OBJECTS:
	C_S
	LIL	R7,RIPPLE_OBJ
	LOAD	R3,R7,RIPPLE_RAD
	LOAD	R4,R7,RIPPLE_X
	LOAD	R5,R7,RIPPLE_Y
	LOAD	R6,R7,RIPPLE_LIFE
	; takes R3=rad,R4,center_x,R5=center_y,R6=char
	TOSUBR	DRAW_CIRCLE
	LIL	R7,RIPPLE_OBJ
	LOAD	R3,R7,RIPPLE_RAD
	LOAD	R6,R7,RIPPLE_LIFE
	ADDSI	R3,1
	ADDSI	R6,-1
	TESTR	R6
	BZR	DO_R6_NOT_ZERO
	LIL	R6,#F
DO_R6_NOT_ZERO:
	STORE	R3,R7,RIPPLE_RAD
	STORE	R6,R7,RIPPLE_LIFE

	R_S

ARSIZE=0
CONVERT_WORD_COORDS:
	;takes R3, the word containing the coords
	;returns R4=x,R5=y
	MOVE	R4,R3
	LIL	R6,#0000FFFF
	AND	R4,R6
	SXT	R4,4*4

	MOVE	R5,R3
	SR	R5,16

	JUMPS	R1	


END

	
